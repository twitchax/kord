letter = { 'A' .. 'G' }

accidental = { "#" | "♯" | "b" | "♭" }

note = { letter ~ accidental? ~ accidental? }

note_atomic = @{ letter ~ accidental? ~ accidental? }

digit = { ASCII_DIGIT }

note_with_octave = { note ~ digit? }

minor = { "-" | "m" }

augmented = { "+" }

diminished = { "o" | "dim" }

half_diminished = { "ø" }

maj7_modifier = { "maj7" }

dominant_modifier = { "7" | "9" | "11" | "13" }

sus_modifier = { "sus2" | "sus4" }

add_modifier = { "add2" | "add4" | "add6" | "6" }

five_modifier = { "b5" | "♭5" | "#5" | "♯5" }

nine_modifier = { "add9" | "b9" | "♭9" | "#9" | "♯9" }

eleven_modifier = { "add11" | "b11" | "♭11" | "#11" | "♯11" }

thirteen_modifier = { "add13" | "b13" | "♭13" | "#13" | "♯13" }

modifier = { sus_modifier | add_modifier | five_modifier | nine_modifier | eleven_modifier | thirteen_modifier }

slash = { "/" }

at = { "@" }

hat = { "^" }

bang = { "!" }

// Mode names - match base modes plus common compound forms
// Most alias resolution is done in Rust normalization
mode_name = @{
    // Special compound mode names (match these first before base names)
    ^"phrygian" ~ " "+ ~ ^"dominant" |
    ^"spanish" ~ " "+ ~ ^"phrygian" |
    ^"lydian" ~ " "+ ~ ^"dominant" |
    ^"aeolian" ~ " "+ ~ ^"dominant" |
    ^"super" ~ " "+ ~ ^"locrian" |
    ^"ultra" ~ " "+ ~ ^"locrian" |
    ^"ultralocrian" |
    ^"half" ~ "-"? ~ ^"diminished" |
    ^"augmented" ~ " "+ ~ ^"major" |
    ^"acoustic" ~ (" "+ ~ ^"scale")? |
    ^"altered" ~ (" "+ ~ ^"scale")? |
    // Base modes with degree modifiers (let Rust normalize symbols)
    ^"locrian" ~ " "+ ~ (^"natural" | ^"nat" | "♮" | ^"sharp" | "#" | "♯") ~ " "* ~ ASCII_DIGIT |
    ^"ionian" ~ " "+ ~ (^"augmented" | ^"sharp" | "#" | "♯") ~ " "* ~ ASCII_DIGIT? |
    ^"dorian" ~ " "+ ~ (^"sharp" | "#" | "♯" | ^"flat" | "b" | "♭") ~ " "* ~ ASCII_DIGIT |
    ^"phrygian" ~ " "+ ~ (^"natural" | ^"nat" | "♮" | ^"major") ~ (" "* ~ ASCII_DIGIT)? |
    ^"lydian" ~ " "+ ~ (^"sharp" | "#" | "♯" | ^"flat" | "b" | "♭" | ^"augmented") ~ " "* ~ ASCII_DIGIT? |
    ^"mixolydian" ~ " "+ ~ (^"sharp" | "#" | "♯" | ^"flat" | "b" | "♭") ~ " "* ~ ASCII_DIGIT |
    ^"major" ~ " "+ ~ (^"sharp" | "#" | "♯") ~ " "* ~ ASCII_DIGIT |
    // Base mode names (unmodified)
    ^"ionian" |
    ^"dorian" |
    ^"phrygian" |
    ^"lydian" |
    ^"mixolydian" |
    ^"aeolian" |
    ^"locrian"
}

// Scale names - simplified, Rust handles most normalization
scale_name = @{
    ^"major" ~ " "? ~ ^"pentatonic" |
    ^"minor" ~ " "? ~ ^"pentatonic" |
    ^"natural" ~ " "? ~ ^"minor" |
    ^"harmonic" ~ " "? ~ ^"minor" |
    ^"melodic" ~ " "? ~ ^"minor" |
    ^"whole" ~ " "? ~ ^"tone" |
    ^"diminished" ~ " "? ~ "(" ~ ^"whole" ~ "-" ~ ^"half" ~ ")" |
    ^"diminished" ~ " "? ~ "(" ~ ^"half" ~ "-" ~ ^"whole" ~ ")" |
    ^"chromatic" |
    ^"blues" |
    ^"major"
}

// Mode or Scale with root note
mode = {
    SOI ~
    note_atomic ~
    mode_name ~
    EOI
}

scale = {
    SOI ~
    note_atomic ~
    scale_name ~
    EOI
}

WHITESPACE = _{ " " }

chord = {
    SOI ~
    note ~
    (maj7_modifier | minor | augmented | diminished | half_diminished)? ~
    (maj7_modifier | dominant_modifier)? ~
    ("("* ~ modifier ~ ")"*)* ~
    (slash ~ note)? ~
    (at ~ digit)? ~
    (hat ~ digit)? ~
    (bang)? ~
    EOI
}