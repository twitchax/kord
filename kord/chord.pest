letter = { 'A' .. 'G' }

accidental = { "#" | "♯" | "b" | "♭" }

note = { letter ~ accidental? ~ accidental? }

note_atomic = @{ letter ~ accidental? ~ accidental? }

digit = { ASCII_DIGIT }

note_with_octave = { note ~ digit? }

minor = { "-" | "m" }

augmented = { "+" }

diminished = { "o" | "dim" }

half_diminished = { "ø" }

maj7_modifier = { "maj7" }

dominant_modifier = { "7" | "9" | "11" | "13" }

sus_modifier = { "sus2" | "sus4" }

add_modifier = { "add2" | "add4" | "add6" | "6" }

five_modifier = { "b5" | "♭5" | "#5" | "♯5" }

nine_modifier = { "add9" | "b9" | "♭9" | "#9" | "♯9" }

eleven_modifier = { "add11" | "b11" | "♭11" | "#11" | "♯11" }

thirteen_modifier = { "add13" | "b13" | "♭13" | "#13" | "♯13" }

modifier = { sus_modifier | add_modifier | five_modifier | nine_modifier | eleven_modifier | thirteen_modifier }

slash = { "/" }

at = { "@" }

hat = { "^" }

bang = { "!" }

// Mode names (case-insensitive would be nice but pest needs explicit)
mode_name = @{
    // Match longest patterns first to avoid conflicts
    ^"locrian" ~ " "+ ~ (^"natural" | ^"nat" | "♮") ~ " "* ~ "6" |
    ^"locrian" ~ " "+ ~ (^"natural" | ^"nat" | "♮") ~ " "* ~ "2" |
    ^"locrian" ~ " "+ ~ (^"sharp" | "#" | "♯") ~ " "* ~ "2" |
    ^"ionian" ~ " "+ ~ (^"sharp" | "#" | "♯") ~ " "* ~ "5" |
    ^"ionian" ~ " "+ ~ ^"augmented" |
    ^"dorian" ~ " "+ ~ (^"sharp" | "#" | "♯") ~ " "* ~ "4" |
    ^"dorian" ~ " "+ ~ (^"flat" | "b" | "♭") ~ " "* ~ "2" |
    ^"phrygian" ~ " "+ ~ ^"dominant" |
    ^"phrygian" ~ " "+ ~ (^"natural" | ^"nat" | "♮") ~ " "* ~ "6" |
    ^"phrygian" ~ " "+ ~ ^"major" |
    ^"lydian" ~ " "+ ~ (^"sharp" | "#" | "♯") ~ " "* ~ "2" |
    ^"lydian" ~ " "+ ~ (^"sharp" | "#" | "♯") ~ " "* ~ "5" |
    ^"lydian" ~ " "+ ~ ^"augmented" |
    ^"lydian" ~ " "+ ~ ^"dominant" |
    ^"lydian" ~ " "+ ~ (^"flat" | "b" | "♭") ~ " "* ~ "7" |
    ^"mixolydian" ~ " "+ ~ (^"sharp" | "#" | "♯") ~ " "* ~ "4" |
    ^"mixolydian" ~ " "+ ~ (^"flat" | "b" | "♭") ~ " "* ~ "6" |
    ^"major" ~ " "+ ~ (^"sharp" | "#" | "♯") ~ " "* ~ "5" |
    ^"augmented" ~ " "+ ~ ^"major" |
    ^"spanish" ~ " "+ ~ ^"phrygian" |
    ^"ultra" ~ " "+ ~ ^"locrian" |
    ^"aeolian" ~ " "+ ~ ^"dominant" |
    ^"half" ~ "-"? ~ ^"diminished" ~ (" "+ ~ (^"natural" | ^"nat" | "♮") ~ " "* ~ "2")? |
    ^"super" ~ " "+ ~ ^"locrian" |
    ^"acoustic" ~ (" "+ ~ ^"scale")? |
    ^"altered" ~ (" "+ ~ ^"scale")? |
    ^"ultralocrian" |
    ^"ionian" |
    ^"dorian" |
    ^"phrygian" |
    ^"lydian" |
    ^"mixolydian" |
    ^"aeolian" |
    ^"locrian"
}

// Scale names (case-insensitive)
scale_name = @{
    ^"major" ~ " "? ~ ^"pentatonic" |
    ^"minor" ~ " "? ~ ^"pentatonic" |
    ^"blues" |
    ^"major" |
    ^"natural" ~ " "? ~ ^"minor" |
    ^"harmonic" ~ " "? ~ ^"minor" |
    ^"melodic" ~ " "? ~ ^"minor" |
    ^"whole" ~ " "? ~ ^"tone" |
    ^"chromatic" |
    ^"diminished" ~ " "? ~ "(" ~ ^"whole" ~ "-" ~ ^"half" ~ ")" |
    ^"diminished" ~ " "? ~ "(" ~ ^"half" ~ "-" ~ ^"whole" ~ ")"
}

// Mode or Scale with root note
mode = {
    SOI ~
    note_atomic ~
    mode_name ~
    EOI
}

scale = {
    SOI ~
    note_atomic ~
    scale_name ~
    EOI
}

WHITESPACE = _{ " " }

chord = {
    SOI ~
    note ~
    (maj7_modifier | minor | augmented | diminished | half_diminished)? ~
    (maj7_modifier | dominant_modifier)? ~
    ("("* ~ modifier ~ ")"*)* ~
    (slash ~ note)? ~
    (at ~ digit)? ~
    (hat ~ digit)? ~
    (bang)? ~
    EOI
}